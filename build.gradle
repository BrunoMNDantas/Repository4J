group 'com.github.brunomndantas'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

repositories {
    mavenCentral()
}

dependencies {
    //JUNIT
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    //COMMONS IO
    implementation 'commons-io:commons-io:2.18.0'

    //JACKSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
    dependsOn(tasks.javadoc) // Ensure it depends on the javadoc task
    archiveClassifier.set('javadoc') // Replaces classifier "javadoc"
    from(tasks.javadoc.destinationDir) // Use destinationDir directly
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.brunomndantas'
            artifactId 'repository4j'
            version '1.0.0'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Repository for Java'
            name 'Repository4J'
            url 'https://github.com/BrunoMNDantas/Repository4J'
            organization {
                name 'com.github.brunomndantas'
                url 'https://github.com/BrunoMNDantas'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/BrunoMNDantas/Repository4J/issues'
            }
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://github.com/BrunoMNDantas/Repository4J/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/BrunoMNDantas/Repository4J'
                connection 'scm:git:git://github.com/BrunoMNDantas/Repository4J.git'
                developerConnection 'scm:git:ssh://git@github.com:BrunoMNDantas/Repository4J.git'
            }
            developers {
                developer {
                    name 'Bruno Dantas'
                }
            }
        }

        //evil unnecessary complicated change to fix packaging type in the pom
        def children = root.children()
        def versionIndex = children.indexOf(children.find {it.name().localPart == 'packaging'})
        children.remove(versionIndex)
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

test {
    useJUnitPlatform()
}